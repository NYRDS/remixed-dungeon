apply plugin: 'com.android.application'

ext.getApiField = { name ->
    def stringsFile = "RemixedDungeon/src/main/res/values/strings_api_signature.xml"
    return new XmlParser().parse(stringsFile).string.find { it.@name.equals name }.text()
}

def jks_keyAlias    = System.getenv('jks_keyAlias') ? System.getenv('jks_keyAlias') : 'testKey'
def jks_keyPassword = System.getenv('jks_keyPassword') ? System.getenv('jks_keyPassword') : '123456'
def jks_storePassword = System.getenv('jks_storePassword') ? System.getenv('jks_storePassword') : '123456'
def jks_storePath = System.getenv('jks_storePath')  ? System.getenv('jks_storePath') : 'testKey/test.jks'

android {

    signingConfigs {
        release {
            keyAlias jks_keyAlias
            keyPassword jks_keyPassword
            storeFile file(jks_storePath)
            storePassword jks_storePassword
        }
    }

    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = ["admob_app_id":getApiField("admob_app_id"),
                                "gow_app_id":getApiField("gow_app_id")]

        applicationId "com.nyrds.pixeldungeon.ml"
        versionCode 1040
        versionName "30.1.fix.17"
        buildToolsVersion = '29.0.2'
        targetSdkVersion 30
        minSdkVersion 16
        compileSdkVersion = 29

        // Enabling multidex support.
        multiDexEnabled true
    }

    aaptOptions {
        noCompress "mp3","ogg"
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            minifyEnabled false
        }
        debug {
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility '1.8'
        targetCompatibility '1.8'
    }

    dexOptions {
        javaMaxHeapSize "4g"
        additionalParameters = ["--set-max-idx-number=45000"]
    }

    lintOptions {
        abortOnError false
    }

    flavorDimensions "platform","market"

    productFlavors {
        googlePlay {
            dimension "market"
            versionCode defaultConfig.versionCode + 10000
        }

        fdroid {
            dimension "market"
        }

        huawei {
            minSdkVersion 19
            dimension "market"
            applicationIdSuffix ".huawei"
        }

        android {
            dimension "platform"

        }

        libgdx {
            dimension "platform"
        }

        none {
            dimension "market"
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'src/android/AndroidManifest.xml'
            //java.srcDirs = ['src/android/java']
            //res.srcDirs = ['src/android/res'] # need to sync transifex first
        }
    }

}



dependencies {
    implementation project(':annotation')

    annotationProcessor project(':processor')

    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.luaj:luaj-jse:3.0.1'
    implementation 'info.guardianproject.netcipher:netcipher:1.2'



    //libgdxImplementation 'com.fancydep.desktop:1.0.7'


    androidImplementation 'com.google.android.ads.consent:consent-library:1.0.8'


    androidImplementation 'androidx.multidex:multidex:2.0.1'

    implementation 'commons-io:commons-io:2.6'

    androidImplementation 'com.appodeal.ads.sdk:core:2.9.2'
    androidImplementation 'com.appodeal.ads.sdk.networks:adcolony:2.9.2.11'
    androidImplementation 'com.appodeal.ads.sdk.networks:applovin:2.9.2.12'
    androidImplementation 'com.appodeal.ads.sdk.networks:bidmachine:2.9.2.7'
    androidImplementation 'com.appodeal.ads.sdk.networks:ironsource:2.9.2.11'
    androidImplementation 'com.appodeal.ads.sdk.networks:my_target:2.9.2.11'
    androidImplementation 'com.appodeal.ads.sdk.networks:unity_ads:2.9.2.8'

    implementation 'org.jetbrains:annotations:15.0'

    annotationProcessor 'org.projectlombok:lombok:1.18.10'
    compileOnly 'org.projectlombok:lombok:1.18.10'

    googlePlayImplementation 'com.google.android.gms:play-services-base:17.6.0'
    googlePlayImplementation 'com.google.android.gms:play-services-auth:19.0.0'
    googlePlayImplementation "com.google.android.gms:play-services-games:21.0.0"

    googlePlayImplementation 'com.android.billingclient:billing:4.0.0'

    googlePlayImplementation platform('com.google.firebase:firebase-bom:26.5.0')


    googlePlayImplementation 'com.google.firebase:firebase-ads'
    googlePlayImplementation 'com.google.firebase:firebase-analytics'
    googlePlayImplementation 'com.google.firebase:firebase-crashlytics'

    huaweiImplementation 'com.huawei.hms:ads-lite:13.4.30.301'
    huaweiImplementation 'com.huawei.hms:hianalytics:5.0.0.300'
    huaweiImplementation 'com.huawei.agconnect:agconnect-crash:1.4.1.300'
}

if (getGradle().getStartParameter().getTaskRequests()
        .toString().contains("GooglePlay")) {
    apply plugin: 'com.google.firebase.crashlytics'
    apply plugin: 'com.google.gms.google-services'
}

if (getGradle().getStartParameter().getTaskRequests()
        .toString().contains("Huawei")) {
    apply plugin: 'com.huawei.agconnect'
}
