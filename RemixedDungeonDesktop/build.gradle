plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'de.undercouch.download' version '5.4.0'
    id 'edu.sc.seis.launch4j' version '3.0.6' // Launch4j plugin for creating .exe files
}

project.ext {
    version = '32.3.alpha'
    version_code = 1236
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_10
    targetCompatibility = JavaVersion.VERSION_1_10
}

jar {
    manifest {
        attributes 'Main-Class': 'com.nyrds.pixeldungeon.desktop.DesktopLauncher'
    }
}

shadowJar {
    archiveBaseName.set('RemixedDungeon')
    archiveVersion.set('')
    archiveClassifier.set('')
    manifest {
        attributes 'Main-Class': 'com.nyrds.pixeldungeon.desktop.DesktopLauncher'
    }
}

application {
    applicationName = 'RemixedDungeon-desktop'
    mainClass = 'com.nyrds.pixeldungeon.desktop.DesktopLauncher'

    sourceSets {
        main {
            java {
                srcDirs = [
                        '../RemixedDungeon/src/main/java',
                        'src/desktop/java',
                        'src/market_none/java',
                        'src/libgdx/java',
                        'src-generated' // New directory for generated sources
                ]
            }
        }
    }
}

tasks.withType(JavaExec).configureEach {
    jvmArgs = ["--add-opens", "java.base/java.util=ALL-UNNAMED", "-Dhttps.protocols=TLSv1.2"]
}

tasks.register('codegen', Exec) {
    commandLine 'python3', 'make_r.py'
    doLast {
        exec {
            commandLine 'python3', 'make_json.py'
        }
    }
}

tasks.register('generateBuildConfig') {
    def version = project.ext.version
    def version_code = project.ext.version_code
    doLast {
        def debug = false
        def buildConfigFile = new File("${projectDir}/src-generated/com/nyrds/pixeldungeon/ml/BuildConfig.java")
        buildConfigFile.parentFile.mkdirs()
        buildConfigFile.text = """
        package com.nyrds.pixeldungeon.ml;

        public class BuildConfig {
            public static final boolean DEBUG = ${debug};
            public static final String SAVES_PATH = "./saves/";
            public static final String FLAVOR_platform = "desktop";
            public static final String FLAVOR_market = "vkplay";
            public static final String VERSION_NAME = "${version}";
            public static final int VERSION_CODE = ${version_code};
        }
        """
    }
}

compileJava.dependsOn generateBuildConfig
compileJava.dependsOn codegen

ext {
    gdxVersion = '1.12.1'
}

tasks.register('dist', Jar) {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

dependencies {
    implementation project(':annotation')
    implementation project(':json_clone')

    compileOnly project(':processor')
    annotationProcessor project(':processor')

    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.luaj:luaj-jse:3.0.1'
    implementation 'org.hjson:hjson:3.1.0'
    implementation 'commons-io:commons-io:2.6'
    implementation 'org.jetbrains:annotations:24.1.0'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    compileOnly 'org.projectlombok:lombok:1.18.30'
    implementation 'com.google.guava:guava:32.0.1-jre'

    implementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
}

// URLs for JDK downloads
def jdkUrls = [
        linux: "https://github.com/adoptium/temurin17-binaries/releases/download/jdk-17.0.14%2B7/OpenJDK17U-jdk_x64_linux_hotspot_17.0.14_7.tar.gz",
        mac: "https://github.com/adoptium/temurin17-binaries/releases/download/jdk-17.0.14%2B7/OpenJDK17U-jdk_aarch64_mac_hotspot_17.0.14_7.tar.gz",
        windows: "https://github.com/adoptium/temurin17-binaries/releases/download/jdk-17.0.14%2B7/OpenJDK17U-jdk_x64_windows_hotspot_17.0.14_7.zip"
]

// Directories for downloaded and minimized JDKs
def jdkDownloadDir = file("${buildDir}/jdk-download")
def jdkMinimizedDir = file("${buildDir}/jdk-minimized")

// Task to download JDKs
tasks.register('downloadJdks') {
    doLast {
        jdkUrls.each { platform, url ->
            def outputFile = new File(jdkDownloadDir, "${platform}.${url.endsWith('.zip') ? 'zip' : 'tar.gz'}")
            outputFile.parentFile.mkdirs()

            if (!outputFile.exists()) {
                println "Downloading JDK for ${platform}..."
                ant.get(src: url, dest: outputFile)
            } else {
                println "JDK for ${platform} already exists. Skipping download."
            }
        }
    }
}

tasks.register('extractJdks') {
    dependsOn 'downloadJdks'
    doLast {
        jdkUrls.each { platform, url ->
            def archiveFile = new File(jdkDownloadDir, "${platform}.${url.endsWith('.zip') ? 'zip' : 'tar.gz'}")
            def outputDir = new File(jdkDownloadDir, platform)
            outputDir.mkdirs()

            if (archiveFile.exists()) {
                if (archiveFile.name.endsWith('.zip')) {
                    copy {
                        from zipTree(archiveFile)
                        into outputDir
                        fileMode 0755
                    }
                } else if (archiveFile.name.endsWith('.tar.gz')) {
                    exec {
                        commandLine 'tar', '-xzf', archiveFile, '-C', outputDir
                    }
                }
            } else {
                logger.warn("Archive for ${platform} does not exist at ${archiveFile}")
            }
        }
    }
}

tasks.register('minimizeJdks') {
    dependsOn 'extractJdks'
    doLast {
        jdkUrls.each { platform, url ->
            def jdkDir = new File(jdkDownloadDir, platform).listFiles().find { it.name.startsWith('jdk') }
            def minimizedJdkDir = new File(jdkMinimizedDir, platform)
            if (minimizedJdkDir.exists()) {
                minimizedJdkDir.deleteDir()
            }

            exec {
                commandLine "${jdkDownloadDir}/linux/jdk-17.0.14+7/bin/jlink",
                        '--module-path', "${jdkDir}/jmods",
                        '--add-modules', 'java.base,java.compiler,java.desktop,java.scripting,java.sql,jdk.unsupported',
                        '--output', minimizedJdkDir,
                        '--strip-debug',
                        '--no-header-files',
                        '--no-man-pages'
            }
        }
    }
}

// Launch4j configuration for creating .exe files
launch4j {
    mainClassName = 'com.nyrds.pixeldungeon.desktop.DesktopLauncher'
    outfile = 'RemixedDungeon.exe'
    icon = "${projectDir}/src/desktop/d_assets/icons/icon.ico"
    productName = 'RemixedDungeon'
    jreMinVersion = '10'
    bundledJrePath = 'jdk'
    stayAlive = false
    dontWrapJar = false
    jarTask = project.tasks.shadowJar
    copyConfigurable = []

    jvmOptions = [
            '--add-opens', 'java.base/java.util=ALL-UNNAMED',
            '-Dhttps.protocols=TLSv1.2'
    ]
}


createExe.dependsOn 'shadowJar'

// Custom tasks for platform-specific bundles
tasks.register('packageLinuxX64', Zip) {
    dependsOn 'minimizeJdks'
    archiveFileName = "RemixedDungeon-${project.ext.version}-linuxX64.zip"
    destinationDirectory = file("../dist")

    from(shadowJar) {
        into "RemixedDungeon-${project.ext.version}-linuxX64"
    }

    from(file("src/desktop/assets")) {
        into "RemixedDungeon-${project.ext.version}-linuxX64/mods/Remixed"
    }

    from(file("src/desktop/d_assets")) {
        into "RemixedDungeon-${project.ext.version}-linuxX64/mods/Remixed"
    }

    from(file("src/desktop/l10ns")) {
        into "RemixedDungeon-${project.ext.version}-linuxX64/mods/Remixed"
    }

    from(new File(jdkMinimizedDir, 'linux')) {
        into "RemixedDungeon-${project.ext.version}-linuxX64/jdk"
    }

    from(file("src/desktop/run.sh")) {
        into "RemixedDungeon-${project.ext.version}-linuxX64"
        fileMode 0755
    }
}

tasks.register('packageMacM1', Zip) {
    dependsOn 'minimizeJdks'
    archiveFileName = "RemixedDungeon-${project.ext.version}-macM1.zip"
    destinationDirectory = file("../dist")

    from(shadowJar) {
        into "RemixedDungeon-${project.ext.version}-macM1"
    }

    from(file("src/desktop/assets")) {
        into "RemixedDungeon-${project.ext.version}-macM1/mods/Remixed"
    }

    from(file("src/desktop/d_assets")) {
        into "RemixedDungeon-${project.ext.version}-macM1/mods/Remixed"
    }

    from(file("src/desktop/l10ns")) {
        into "RemixedDungeon-${project.ext.version}-macM1/mods/Remixed"
    }

    from(new File(jdkMinimizedDir, 'mac')) {
        into "RemixedDungeon-${project.ext.version}-macM1/jdk"
    }

    from(file("src/desktop/run-mac.sh")) {
        into "RemixedDungeon-${project.ext.version}-macM1"
        fileMode 0755
    }
}

tasks.register('packageWinX64', Zip) {
    dependsOn 'minimizeJdks', 'createExe'
    archiveFileName = "RemixedDungeon-${project.ext.version}-winX64.zip"
    destinationDirectory = file("../dist")

    from(file("src/desktop/assets")) {
        into "RemixedDungeon-${project.ext.version}-winX64/mods/Remixed"
    }

    from(file("src/desktop/d_assets")) {
        into "RemixedDungeon-${project.ext.version}-winX64/mods/Remixed"
    }

    from(file("src/desktop/l10ns")) {
        into "RemixedDungeon-${project.ext.version}-winX64/mods/Remixed"
    }

    from(new File(jdkMinimizedDir, 'windows')) {
        into "RemixedDungeon-${project.ext.version}-winX64/jdk"
    }

    from("${projectDir}/build/launch4j/RemixedDungeon.exe") {
        into "RemixedDungeon-${project.ext.version}-winX64"
    }
}

tasks.register('buildAllBundles') {
    dependsOn 'packageLinuxX64', 'packageMacM1', 'packageWinX64'
}