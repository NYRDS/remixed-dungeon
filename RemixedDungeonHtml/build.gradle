plugins {
    id 'java'
    id 'org.teavm' version '0.12.3'
}

project.ext {
    version = '32.3.alpha'
    version_code = 1236
    teaVmVersion = '0.12.3'
    gdxVersion = '1.12.1'
    gdxTeaVMVersion = '1.3.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

sourceSets {
    main {
        java {
            srcDirs = [
                    'src/html/java',                    // HTML sources first (higher priority)
                    '../RemixedDungeon/src/main/java',  // Main sources second
                    'src/market_none/java',
                    'build/generated/sources/annotationProcessor/java/main'
            ]
        }
        resources {
            srcDirs = [
                    'src/main/resources',
                    'src/html/java',
                    'com'
            ]
        }
    }
}

// Create a task to generate the BuildConfig.java file
tasks.register('generateBuildConfig') {
    def version = project.ext.version
    def version_code = project.ext.version_code
    doLast {
        def debug = false
        def buildConfigFile = new File("${projectDir}/src/html/java/com/nyrds/pixeldungeon/ml/BuildConfig.java")
        buildConfigFile.parentFile.mkdirs()
        buildConfigFile.text = """
        package com.nyrds.pixeldungeon.ml;

        public class BuildConfig {
            public static final boolean DEBUG = ${debug};
            public static final String SAVES_PATH = "./saves/";
            public static final String FLAVOR_platform = "html";
            public static final String FLAVOR_market = "vkplay";
            public static final String VERSION_NAME = "${version}";
            public static final int VERSION_CODE = ${version_code};
        }
        """
    }
}

tasks.register('codegen', Exec) {
    commandLine 'python3', 'make_r.py'
    doLast {
        exec {
            commandLine 'python3', 'make_json.py'
        }
    }
}

// Ensure BundleHelper is generated before compilation
compileJava.dependsOn generateBuildConfig
compileJava.dependsOn codegen
compileJava.dependsOn ':processor:compileJava'

repositories {
    mavenCentral()
    google()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url "https://teavm.org/maven/repository/" }
    maven { url "https://jitpack.io" } // For gdx-teavm
}


dependencies {
    implementation project(':annotation')
    
    implementation project(':json_clone')
    
    compileOnly project(':processor')
    annotationProcessor project(':processor')
    
    implementation 'com.google.code.gson:gson:2.10.1'
    
    implementation 'org.luaj:luaj-jse:3.0.1'
    implementation 'org.hjson:hjson:3.1.0'
    implementation 'commons-io:commons-io:2.6'
    implementation 'org.jetbrains:annotations:24.1.0'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    compileOnly 'org.projectlombok:lombok:1.18.30'
    implementation 'com.google.guava:guava:32.0.1-jre'

    // LibGDX dependencies for HTML backend
    implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx:$gdxVersion:sources"
    implementation "com.github.xpenatan.gdx-teavm:backend-teavm:$gdxTeaVMVersion" // Correct dependency name
    implementation "org.teavm:teavm-classlib:$teaVmVersion"
    implementation "org.teavm:teavm-jso-apis:$teaVmVersion"
    implementation "org.teavm:teavm-interop:$teaVmVersion"

    compileOnly "org.teavm:teavm-core:$teaVmVersion"
    annotationProcessor "org.teavm:teavm-core:$teaVmVersion"

    implementation files('libs/gdx-backend-gwt-fixed-complete-modified.jar')
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
}

// TeaVM configuration - explicit configuration
teavm {
    js {
        mainClass = 'com.nyrds.platform.app.client.TeaVMLauncher'
        targetFileName = 'teavm-app.js'
        
        properties.put('org.teavm.reflect.enableRef', 'true')
    }
}
